Grammar:		DataElement 5.0
Namespace:		shr.allergy
Description:	"The SHR Allergy domain contains definitions for statements dealing with substance-related risks, including allergies and intolerances."
Uses:			shr.actor, shr.core, shr.base, shr.adverse, shr.condition, shr.observation

CodeSystem:     SCT = http://snomed.info/sct



				
EntryElement:	AllergyIntolerance
Based on:		HealthState
Concept:		SCT#420134006 "Propensity to adverse reactions"	
Description:	"A finding related to the presence or absence of an individual's risk or sensitivity to a substance or class of substances. A finding can be taken as tantamount to a representation of an allergic condition, allowing it to be tracked over time."
Value:			CodeableConcept from AllergenIrritantVS
0..*			SubstanceCategory
1..1			VerificationStatus
0..1			AdverseReactionType 
0..1			Criticality from http://hl7.org/fhir/ValueSet/allergy-intolerance-criticality
0..*			AdverseReaction  // not ref() because this maps to FHIR
0..1			Onset
0..1			Abatement
0..1			MostRecentOccurrenceTime

		Element:		SubstanceCategory
		Concept:		TBD
		Description:	"Categorization of the risk substance as a food, drug, or environmental agent. For difficult-to-classify substances, one can leave this field empty or choose the most typical category."
		Value:			code from http://hl7.org/fhir/ValueSet/allergy-intolerance-category

		Element:		VerificationStatus
		Concept:		TBD
		Description:	"Whether an assessment has been confirmed by testing or observation."
		Value:			code from http://hl7.org/fhir/ValueSet/allergy-verification-status

		Element:		AdverseReactionType
		Concept:		TBD
		Description:	"Identification of the underlying physiological mechanism for a Reaction Risk."
		Value:			code from http://hl7.org/fhir/ValueSet/allergy-intolerance-type

		Element:		MostRecentOccurrenceTime
		Concept:		TBD
		Description:	"The time of the last or latest of a series of events."
		Value:			dateTime


/*				
EntryElement:	NoKnownAllergyIntoleranceToSubstance
Based on:		HealthStateAbsentOrNotSuspected
Concept:		TBD
Description:	"Used to record that a particular substance or class of substances does not pose a known elevated risk to the subject."
Value:			CodeableConcept from AllergenIrritantVS
*/
	
EntryElement:	NoKnownAllergy
Based on:		HealthStateAbsentOrNotSuspected
Concept:		MTH#C0262580
Description:	"Express no known allergies to broad classes: anything, animals, drugs, foods, or environmental factors."
Value:			CodeableConcept from NoKnownAllergyVS
