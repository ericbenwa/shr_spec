Grammar:		DataElement 5.0
Namespace:		oncocore
Description:	"SHR implementation of ASCO requirements."
Uses:  			shr.core


EntryElement:   TumorPresent
Based on:		BodyStructurePresent
Concept:        SCT#395557000 "Tumor finding (finding)"
Description:    "The presence of an abnormal mass of tissue (neoplasm) that results when cells divide more than they should or do not die when they should. Tumors may be benign (not cancer), or malignant (cancer). (source: NCI Dictionary)."
                Code from CancerHistologicTypeVS
// note - the code represents the morphology, as distinct from the location in BodySite
0..0			Severity
0..0			StageInformation
0..1            IsPrimaryTumor

        Element:        IsPrimaryTumor
        Concept:        TBD
        Description:    "Whether the tumor is the original or first tumor in the body, for a particular cancer."
        Value:          CodeableConcept from YesNoUnknownVS


EntryElement: 	TumorDimensions
Based on:		CodedLaboratoryObservation
0..0			DataValue
	            Focus value is type ref(TumorPresent)
				Code is SCT#263605001 "Tumor size (observable entity)"
0..0            PanelMembers
				Components.ObservationComponent
					includes 0..1	TumorLongestDimension
					includes 0..1	TumorDimension2
					includes 0..1   TumorDimension3
					includes 0..1	SizeOfGrossTumorBed

		Element:		TumorLongestDimension
		Description:	"The longest dimension of the tumor."
		Based on:		QuantitativeObservationComponent
		Concept:		LNC#33728-7 "Size.maximum dimension in Tumor"
                        Code is LNC#33728-7 "Size.maximum dimension in Tumor"
		                DataValue.Quantity.Units is UCUM#mm		
		
		Element:		TumorDimension2
		Description:	"The second longest dimension of the tumor, generally measured perpendicularly to the tumor's longest dimension."
		Based on:		QuantitativeObservationComponent
		Concept:		SCT#372300005 "Tumor size, dimension 2 (observable entity)"
                        Code is SCT#372300005 "Tumor size, dimension 2 (observable entity)"
		                DataValue.Quantity.Units is UCUM#mm	

		Element:		TumorDimension3
		Description:	"The third longest dimension of the tumor, generally measured perpendicularly to the tumor's two longest perpendicular dimensions."
		Based on:		QuantitativeObservationComponent
		Concept:		SCT#372301009 "Tumor size, dimension 3 (observable entity)"
                        Code is SCT#372301009 "Tumor size, dimension 3 (observable entity)"
		                DataValue.Quantity.Units is UCUM#mm	

		Element:		SizeOfGrossTumorBed
		Based on:		QuantitativeObservationComponent
		Description:	"The largest dimension of the gross tumor bed/fibrotic area."
                        Code is TBD#TBD
		                DataValue.Quantity.Units is UCUM#mm	

EntryElement:	PrimaryTumorPresent
Based on:		TumorPresent
Description:	"The presence of a tumor considered the original, or first tumor associated with a particular cancer. (adapted from NCI Dictionary of Cancer Terms)."
1..1			IsPrimaryTumor is #Yes


EntryElement:	TumorDimensionsPrimaryTumor
Based on:		TumorDimensions
Concept:		SCT#399734001 "Primary tumor size (observable entity)"
Description:	"The dimensions associated with the primary tumor."
	            Focus value is type ref(PrimaryTumorPresent)
1..1			Components.ObservationComponent.TumorLongestDimension


EntryElement: 	TumorMargins
Based on:		CodedLaboratoryObservation
Concept:		MTH#C4086369
Description:	"The edge or border of the tissue removed in cancer surgery. The margin is described as negative or clean when the pathologist finds no cancer cells at the edge of the tissue, suggesting that all of the cancer has been removed. The margin is described as positive or involved when the pathologist finds cancer cells at the edge of the tissue, suggesting that all of the cancer has not been removed."
                DataValue from PositiveNegativeVS
				Code is MTH#C4086369
0..0            PanelMembers
				Components.ObservationComponent
					includes 0..1	TumorMarginDescription
// Do we need to record the method? whether the result of imaging, or study of a removed tumor after lumpectomy?	

		Element:		TumorMarginDescription
		Based on:		CodedObservationComponent
		Description:	"Description of the edge or border of tumor in situ by radiologist or of removed tumor by pathologist."
						Code is TBD#TBD
		                DataValue from TumorMarginDescriptionVS

/*
Element:		TumorCellularity
Concept:		MTH#C4055283
Based on:		QuantitativeObservationComponent
Description:	"Percentage of cells in a sample that are cancerous"
				DataValue.Quantity.Units is UCUM#%


Element:		PercentageOfInSituDisease
Based on:		QuantitativeObservationComponent
Concept:		TBD
Description:	"The percentage of the cancer that is in situ, as opposed to invading other tissues."
			    DataValue.Quantity.Units is UCUM#%
*/
